intchan10 cc;
intchan10 dd;
boolchan ff; 
 
void factorial(boolchan f, intchan d, intchan c){ 
   int j,n,z; 
   bool run = true;  
   while (run) { // Ewiges Leben?
      select {
         case  <? f:{ // Aha, Zeit aufzuhoeren.
            run = false;
         } 
         case z = <? d:{ // Aha, Weitermachen.
            n=1;
            for (j = z; j > 0; j--){
               n= n*j; 
            }   
            c <! n; // Ich sende, was rauskommt.
         }          // Und fange wieder von vorne an.
      }
	}
    println("Kein ewiges Leben"); 
}

mainAgent{
   int j;
   agent[4] a = {
   		start factorial(ff, dd, cc),
   		start factorial(ff, dd, cc),
   		start factorial(ff, dd, cc),
   		start factorial(ff, dd, cc)
   };
   for (j=1; j<=8 ; j++){
       dd <! j;                           // Hier gibt's Arbeit.
   }                                      // Jetzt ham alle viel zu tun.
   int res;
   for (j=1; j<=8 ; j++){
       res = <? cc;                         // Hier sammel ich die Ergebnisse ein.
       println("Eine der Fakultaeten von 1 bis 8 ist " + res + "."); // Ui.
   }  
   for (j=0; j<4 ; j++){
       ff <! true;                        // Jetzt ist Schluss fuer euch.
   }                                        
}


